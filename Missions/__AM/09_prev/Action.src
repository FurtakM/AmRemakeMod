Export Function Action;
var x, y, i, tmp, t, engs, un;
begin
InGameOn;

x := 101;
y := 116;


PlaceSeeing(x, y, 1, -30);
CenterNowOnXY(x, y);

tmp := FilterAllUnits([[f_side, 4], [f_type, unit_human]]) diff Sikorski;

AddComMoveXY(Sikorski, 109, 120);

wait(0$02);

AddComAgressiveMove(tmp, 108, 117);

repeat
 wait(0$01);
until See(3, Sikorski);

un := UnitsInside(NearestUnitToUnit(FilterAllUnits([f_btype, b_breastwork]), Sikorski))[1];

ComExitBuilding(un);
AddComAttackUnit(un, Sikorski);

repeat
 wait(0$01);
until IsDying(un);

ComMoveXY(tmp, 109, 119);
AddComTurnXY(tmp, 111, 125);

Say(Sikorski, 'D1-Sik-1');

engs := FilterAllUnits([f_class, 2]);

ComExitBuilding(engs);

for i = 1 to engs do
    AddComEnterUnit(engs[i], FilterAllUnits([f_weapon, ru_cargo_bay])[i]);

AddComMoveXY(engs, 148, 208);

Say(engs[1], 'D1-Rus1-1');

un := FilterAllUnits([[f_side, 3], [f_class, 1]]);

ComExitBuilding(un);
AddComMoveXY(un, 148, 208);

Wait(5);

Say(FilterAllUnits([[f_side, 4], [f_sex, sex_female]])[1], 'D1-FSol-1');
Say(Sikorski, 'D1-Sik-2');

ComMoveXY(Sikorski, 111, 122);

repeat
 wait(0$01);
until GetDistUnitXY(Sikorski, 111, 122) < 3 or GetLives(Sikorski) < 250;

ComMoveXY(Sikorski, 114, 136);


t := [   [104,117],
         [115,123],
         [114,126],
         [111,122],
         [112,126],
         [117,130],
         [106,127],
         [111,117],
         [98, 119],
         [116,131],
         [109,129],
         [100,113]
     ];

for i in t do
    begin
    wait(0$0.3);
    MineExplosion(i[1], i[2], 1);
    end;

MineExplosion(GetX(Sikorski), GetY(Sikorski), 4);
KillUnit(Sikorski);

ComMoveXY(tmp, 109, 124);

wait(0$2);

SaveCharacters(tmp diff [Gary, Bobby, Cyrus], '09_prev_squad');

tmp := [];

for i in FilterAllUnits([f_type, unit_building]) do
    tmp := tmp ^ [GetBType(i), GetX(i), GetY(i), GetDir(i), GetLives(i)];

SaveVariable(tmp diff 0, '09_ovsyenko_base');

ChangeMap(1, '__am/09');

InGameOff;
End;

Every 0$1 do
var i, filter;
begin
enable;

filter := FilterAllUnits([f_side, 4]) diff Sikorski;

for i in filter do
    if GetLives(i) < 1000 then
       SetLives(i, 1000);
End;



