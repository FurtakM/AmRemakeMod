Export Function Action;
var i, units, vehicles, cargos, resources, cargo;
begin
InGameOn;

CenterNowOnUnits(JMM);

ComTurnUnit(JMM, Cornel);

if Bierezov then
   ComTurnUnit(Bierezov, Cornel);

for i in jmm_units do
    ComTurnUnit(i, Cornel);

units := cornel_units union Cornel;

repeat
 wait(1);

 for i in units do
     ComMoveXY(i, GetX(JMM), GetY(JMM));

until UnitFilter(units, [f_distxy, GetX(JMM), GetY(JMM), 10]) = units;

for i in units do
    ComTurnUnit(i, JMM);

ComTurnUnit(Cornel, JMM);

Say(JMM, 'D1-JMM-1');
Say(Cornel, 'D1-Corn-1');

ComMoveUnit(JMM, Cornel);
ComMoveUnit(Cornel, JMM);

repeat
 wait(0$01);
until GetDistUnits(JMM, Cornel) < 6;

ChangeSideFog(4, 1);

ComTurnUnit(JMM, Cornel);
ComTurnUnit(Cornel, JMM);

Say(JMM, 'D1-JMM-2');
Say(JMM, 'D1-JMM-2a');
Say(Cornel, 'D1-Corn-2');

if bierezov_exist or debug then
   begin
   ComTurnUnit(Cornel, Bierezov);
   Wait(0$0.3);
   Say(Cornel, 'D1a-Corn-1');

   ComTurnUnit(JMM, Bierezov);
   ComTurnUnit(Bierezov, JMM);

   Say(JMM, 'D1a-JMM-1');

   ComTurnUnit(JMM, Cornel);
   ComTurnUnit(Cornel, JMM);

   Say(Cornel, 'D1a-Corn-2');
   Say(JMM, 'D1a-JMM-2');
   Say(Cornel, 'D1a-Corn-3');
   Say(JMM, 'D1a-JMM-3');
   Say(Cornel, 'D1a-Corn-4');
   Say(JMM, 'D1a-JMM-4');
   Say(Cornel, 'D1a-Corn-5');

   ComMoveXY(Bierezov, GetX(Cornel), GetY(Cornel)-2);
   AddComTurnUnit(Bierezov, Cornel);

   Wait(0$0.3);
   end;

Say(JMM, 'D1b-JMM-1');
Say(Cornel, 'D1b-Corn-1');
Say(JMM, 'D1b-JMM-2');
Say(Cornel, 'D1b-Corn-2');
Say(JMM, 'D1b-JMM-3');

Wait(0$0.3);

SayRadio(Powell, 'D1b-Pow-3');
Say(JMM, 'D1b-JMM-4');
Say(Cornel, 'D1b-Corn-4');

if Khatam then
   Say(Khatam, 'D1b-Khat-4')
  else
   SayX(UnitFilter(cornel_units, [f_sex, sex_male]) diff [Cornel], 'D1b-Sol1-4');

if Cyrus then
   Say(Cyrus, 'D1b-Cyrus-4');

if Lisa then
   begin
   Say(Lisa, 'D1b-Lisa-4');

   if Cyrus then
      begin
      if not IsInUnit(Cyrus) then
         ComTurnUnit(Cyrus, Lisa);

      Say(Cyrus, 'D1b-Cyrus-5');
      end;
   end;

SelectGroup;


Say(JMM, 'D1d-JMM-1');
Say(Cornel, 'D1d-Corn-1');

for i in jmm_units^cornel_units^JMM do
    ComHold(i);

vehicles := FilterAllUnits([[f_side, 1], [f_type, unit_vehicle]]);

if vehicles then
   begin

   if UnitFilter(cornel_units, [f_driving]) then
      for i in UnitFilter(cornel_units, [f_driving]) do
          ComExitVehicle(i);

   cargos := UnitFilter(vehicles, [f_weapon, ru_cargo_bay]);

   if cargos then
      begin
      vehicles := cargos;
      resources := LoadVariable('02_resources_4', 0);

      if debug and not resources then
         resources := 160;

      if resources mod 10 then
         resources := resources - resources mod 10;

      if resources then
         for i in cargos do
             begin
             if resources < 100 then
                begin                 
                cargo := resources;
                resources := 0;
                end
               else
                begin
                cargo := 100;
                resources := resources - 100;
                end;

             SetCargo(i, mat_cans, cargo);

             if resources = 0 then
                break;
             end;
      end;

   ComExitVehicle(IsDrivenBy(vehicles[1]));
   SetSide(vehicles[1], 4);
   ComEnterUnit(Cornel, vehicles[1]);

   repeat
    wait(0$01);
   until IsInUnit(Cornel);

   end;

InGameOff;

ChangeMissionObjectives('M1');

cornel_active := true;
End;

// West Base
Every 0$01 trigger GetDistUnits(JMM, Lynch) < 10 do
var i, points, sol, buns;
begin
points := [ [89, 34], [138, 63], [196, 84], [135, 52], [103, 39], [58, 30], [38, 51] ];

InGameOn;

if jmm_units then
   for i in jmm_units do
       begin
       if GetDistUnits(i, JMM) < 10 then
          ComTurnUnit(i, JMM)
           else
            ComHold(i);
       end;

ComMoveUnit(JMM, Lynch);

repeat
 wait(0$01);
until GetDistUnits(JMM, Lynch) < 6;

ComTurnUnit(JMM, Lynch);

for i in [Lynch, Walker, Turner, Jillian] do
    ComTurnUnit(i, JMM);

Wait(0$0.3);

Say(JMM, 'D2-JMM-1');
Say(Lynch, 'D2-Sol1-1');
Say(JMM, 'D2-JMM-2');
Say(Lynch, 'D2-Sol1-2');
Say(JMM, 'D2-JMM-3');
Say(Lynch, 'D2-Sol1-3');

for i in FilterAllUnits([f_side, 8]) do
    SetSide(i, 1);

Say(JMM, 'D2-JMM-4');

// camera on
RevealFogArea(1, roadArea);

for i = 1 to points do
    begin
    CenterOnXY(points[i][1], points[i][2]);

    if i = 1 then
       Say(Lynch, 'D2-Sol1-4');

    if i = 2 then
       Say(JMM, 'D2-JMM-5');

    if i = 4 then
       begin
       RevealFogArea(1, troopsArea);

       Say(Lynch, 'D2-Sol1-5');
       end;

    if i = 5 then
       Say(JMM, 'D2-JMM-6');

    if i = 7 then
       begin
       RevealFogArea(1, forestArea);

       Say(Lynch, 'D2-Sol1-6');
       end;

    Wait(0$1.3);
    end;

CenterNowOnUnits(JMM);

Say(JMM, 'D2-JMM-7');
Say(Lynch, 'D2-Sol1-7');
Say(JMM, 'D2-JMM-8');


buns := FilterAllUnits([[f_side, 1], [f_btype, b_breastwork]]);

ComEnterUnit(Lynch, buns[1]);

sol := NearestUnitToUnit(UnitFilter(jmm_units, [f_class, 1]), JMM);

if sol then
   if GetDistUnits(JMM, sol) < 10 then
      ComEnterUnit(sol, buns[2]);

Wait(0$0.3);

ComMoveXY(JMM, 65, 101);
AddComTurnXY(JMM, 63, 100);

repeat
 wait(0$01);
until IsAt(JMM, 65, 101);

Say(JMM, 'D2a-JMM-1');

ComMoveXY(Walker, 66, 103);

repeat
 wait(0$01);
until IsAt(Walker, 66, 103);

ComTurnUnit(Walker, JMM);

Say(Walker, 'D2a-Sci1-1');

ComTurnUnit(JMM, Walker);

Say(JMM, 'D2a-JMM-2');
Say(Walker, 'D2a-Sci1-2');
Say(JMM, 'D2a-JMM-3');
Say(Walker, 'D2a-Sci1-3');

jmm_units := jmm_units ^ [Lynch, Walker, Turner, Jillian];

for i in jmm_units do
    if not IsInUnit(i) then
       ComFree(i);

InGameOff;

ChangeMissionObjectives('MSolar1');

jmm_on_west := true;

Wait(0$30);

frank_can_return := true;
End;

Every 0$01 trigger FilterAllUnits([[[f_side, 1], [f_weapon, ru_cargo_bay], [f_distxy, 63, 100, 5]]]) and not jmm_on_west do
var i, filter;
begin
enable;

filter := FilterAllUnits([[[f_side, 1], [f_weapon, ru_cargo_bay], [f_distxy, 63, 100, 5]]]);

if not filter then
   exit;

for i in filter do
    begin
    SetFuel(i, 0);
    ComStop(i);
    end;
End;


// frank return
Every 0$10 trigger frank_can_return do
var i, points;
begin
uc_side := 8;
points := [ [59, 71], [122, 117] ];
Frank := PrepareUnit('Frank', false);

i := rand(1,2);

PlaceUnitXY(Frank, points[i][1], points[i][2], false);

ComMoveUnit(Frank, us_dep_west);

repeat
 wait(0$01);
until GetDistUnits(Frank, JMM) < 8;

InGameOn;

if IsInUnit(JMM) then
   ComFree(JMM);

ComMoveUnit(JMM, Frank);
ComMoveUnit(Frank, JMM);

Say(JMM, 'D6-JMM-1');

repeat
 wait(0$01);
until GetDistUnits(JMM, Frank) < 8;

if Lisa and GetDistUnits(Lisa, Frank) < 20 then
   begin
   ComFree(Lisa);
   AddComMoveUnit(Lisa, Frank);
   end;

if Lynch then
   begin
   ComFree(Lynch);
   AddComMoveUnit(Lynch, Frank);
   end;

ComTurnUnit(JMM, Frank);
ComTurnUnit(Frank, JMM);

Say(Frank, 'D6-Frank-1');

if Lisa and GetDistUnits(Lisa, Frank) < 20 and IsOk(Lisa) then
   begin
    repeat
     wait(0$01);
    until GetDistUnits(Lisa, Frank) < 7;

   Say(Lisa, 'D6-Lisa-1');

   ComTurnUnit(Lisa, Frank);
   ComTurnUnit(Frank, Lisa);

   Say(Frank, 'D6-Frank-2');
   end;

if Lynch and GetDistUnits(Lynch, Frank) < 20 and IsOk(Lynch) then
   begin
   ComTurnUnit(Lynch, JMM);
   ComTurnUnit(Frank, JMM);

   Say(Lynch, 'D6-Sol1-2');
   Say(JMM, 'D6-JMM-2');
   Say(Frank, 'D6-Frank-3');
   Say(JMM, 'D6-JMM-3');
   Say(Frank, 'D6-Frank-4');
   Say(Frank, 'D6-Frank-4a');
   Say(JMM, 'D6-JMM-4');
   Say(Frank, 'D6-Frank-5');

   if Lisa and IsOk(Lisa) then
      Say(Lisa, 'D6-Lisa-5');

   Say(Frank, 'D6-Frank-6');
   Say(JMM, 'D6-JMM-6');
   end
    else
     begin
     ComTurnUnit(Frank, JMM);

     Say(Frank, 'D6-Frank-4');
     Say(Frank, 'D6-Frank-4a');
     Say(JMM, 'D6-JMM-4');
     Say(Frank, 'D6-Frank-5');

     if Lisa and IsOk(Lisa) then
        Say(Lisa, 'D6-Lisa-5');

     Say(Frank, 'D6-Frank-6');
     Say(JMM, 'D6-JMM-6');
     end;

Case Query('Q1') of
1: frank_send_to_scout := true;
2: frank_send_to_scout := false;
End;

InGameOff;

ComFree([JMM, Lisa, Lynch]);

if frank_send_to_scout then
   begin
   ComMoveXY(Frank, 130, 123);

   repeat
    wait(0$01);
   until not See(1, Frank);

   Wait(0$02);

   RemoveUnit(Frank);
   end
    else
     SetSide(Frank, 1);


Wait([12$00, 11$00, 10$00][Difficulty]);
cornel_prepared := true;
End;

Every 0$01 trigger cornel_prepared do
begin
SayRadio(Cornel, 'D3-Corn-1');

repeat
 wait(0$01);
until cornel_counter = 0;

SayRadio(Cornel, 'D3a-Corn-1');
Say(JMM, 'D3a-JMM-1');

end_mission_allowed := true;
ChangeMissionObjectives('M2');
SetAreaMapShow(endMArea, 1);

Wait(0$05);

SayRadio(Cornel, 'D3a-Corn-2');

cornel_attack := true;

if frank_send_to_scout then
   begin
   InitHc;
   InitUc;

   uc_side := 8;
   Frank := PrepareUnit('Frank', false);
   PlaceUnitXY(Frank, 6, 9, false);
   ComCrawl(Frank);

   repeat
    wait(0$01);
   until See(1, Frank);

   SetSide(Frank, 1);
   Say(Frank, 'D6a-Frank-1');
   end;
End;

// vehicle builded
Every 0$01 trigger solar_builded do
begin
Wait(0$02);

DialogueOn;

Say(JMM, 'D2b-JMM-1');

if Walker and IsOk(Walker) then
   begin
   Say(Walker, 'D2b-Sci1-1');
   Say(JMM, 'D2b-JMM-2');
   Say(Walker, 'D2b-Sci1-2');
   Say(JMM, 'D2b-JMM-3');
   end;

DialogueOff;

ChangeMissionObjectives('MOutSol');
End;

Every 0$01 trigger solar_builded and IsOk(JMM) and InVeh(JMM) and not jmm_in_veh do
var i;
begin
jmm_in_veh := true;

i := rand(0,1);

Wait(0$02);

if i then
   Say(JMM, 'D2c-JMM-1')
  else
   Say(JMM, 'D2c-JMM-1a');
End;

Every 0$01 trigger solar_builded and IsOk(Bobby) and InVeh(Bobby) and not bobby_in_veh do
begin
bobby_in_veh := true;

Wait(0$02);

Say(Bobby, 'D2c-Bobby-1')
End;

Every 0$01 trigger solar_builded and IsOk(Lisa) and InVeh(Lisa) and not lisa_in_veh do
begin
lisa_in_veh := true;

Wait(0$02);

Say(Lisa, 'D2c-Lisa-1')
End;

Every 0$01 trigger solar_builded and IsOk(Cyrus) and InVeh(Cyrus) and not cyrus_in_veh do
var i;
begin
cyrus_in_veh := true;

i := rand(0,1);

Wait(0$02);

if i then
   Say(Cyrus, 'D2c-Cyrus-1')
  else
   Say(Cyrus, 'D2c-Cyrus-1a');
End;


// end mission
Every 0$01 trigger FilterUnitsInArea(endMArea, [f_side, 1]) do
var i, filter;
begin
enable;

filter := FilterUnitsInArea(endMArea, [f_side, 1]);

if not filter then
   exit;

for i in filter do
    begin

    if IsDrivenBy(i) and GetType(i) = unit_human then
       begin
       ComExitVehicle(i);
       Wait(3);
       end;

    // macmilan on end
    if i = JMM then
       begin

       if show_query and FilterAllUnits([[f_side, 1], [f_type, unit_human]]) > 1 then
          begin
          show_query := false;

          Case Query('Q2') of
          1: wait_for_them := true;
          2: wait_for_them := false;
          end;

          end;

       if not wait_for_them or FilterAllUnits([[f_side, 1], [f_type, unit_human]]) = 1 then
          begin
          save_group := save_group ^ JMM;
          RemoveUnit(JMM);
          EndMission;
          end;

       end; // end macmilan

    if i = Lisa then
       begin
       save_group := save_group ^ Lisa;
       RemoveUnit(Lisa);
       end;

    if i = Bobby then
       begin
       save_group := save_group ^ Bobby;
       RemoveUnit(Bobby);
       end;

    if i = Cyrus then
       begin
       save_group := save_group ^ Cyrus;
       RemoveUnit(Cyrus);
       end;

    if i = Khatam then
       begin
       save_group := save_group ^ Khatam;
       RemoveUnit(Khatam);
       end;

    if i = Jerry then
       begin
       save_group := save_group ^ Jerry;
       RemoveUnit(Jerry);
       end;

    if i = Brian then
       begin
       save_group := save_group ^ Brian;
       RemoveUnit(Brian);
       end;

    if i = Lynch then
       begin
       save_group := save_group ^ Lynch;
       RemoveUnit(Lynch);
       end;

    if i = Turner then
       begin
       save_group := save_group ^ Turner;
       RemoveUnit(Turner);
       end;

    if i = Jillian then
       begin
       save_group := save_group ^ Jillian;
       RemoveUnit(Jillian);
       end;

    if i = Walker then
       begin
       save_group := save_group ^ Walker;
       RemoveUnit(Walker);
       end;

    if i = Frank then
       begin
       save_group := save_group ^ Frank;
       RemoveUnit(Frank);
       end;

    if IsOk(i) and GetType(i) = unit_human and not i in save_group then
       begin
       save_others := save_others ^ i;
       RemoveUnit(i);
       end;

    if IsOk(i) and GetType(i) = unit_vehicle then
       begin
       veh_on_meta := true;
       RemoveUnit(i);
       end;

    end;
    

End;


Export Function EndMission;
var counter;
begin
// medals
if solar_builded then
   AddMedal('Solar1', 1)
  else
   AddMedal('Solar1', -1);

if veh_on_meta then
   AddMedal('Solar2', 1)
  else
   if solar_builded then
      AddMedal('Solar2', -1)
     else
      AddMedal('Solar2', -2);

counter := save_group ^ save_others;

if counter = 10 then
   AddMedal('No', 1)
    else
     if counter < 10 and counter > 6 then
        AddMedal('No', -1)
         else
          AddMedal('UpTo4', -1);

GiveMedals('MAIN');

RewardPeople(save_group^save_others);

SaveCharacters(JMM, 'JMM');

if Bobby in save_group then
   SaveCharacters(Bobby, 'Bobby');

if Cyrus in save_group then
   SaveCharacters(Cyrus, 'Cyrus');

if Lisa in save_group then
   SaveCharacters(Lisa, 'Lisa');

if Frank in save_group then
   SaveCharacters(Frank, 'Frank');

if Khatam in save_group then
   SaveCharacters(Khatam, 'Khatam');

if save_others then
   SaveCharacters(save_others, '03_others');

// todo

YouWin;
End;


