Export Pokryshkin;
Export ru_patrol, ru_spec_patrol, ru_forest, ru_vehicles;
Export ru_firepoints_south, ru_alert, ru_alert_xy;
Export Function PrepareRussian;
var i, b, un, veh, tw, bar, skill, vehicles, spot_xy;
begin
ru_alert := false;

tw := FilterAllUnits([[f_side, 3], [f_or, [f_btype, b_breastwork], [f_btype, b_bunker]]]);
bar := FilterAllUnits([[f_side, 3], [f_btype, b_armoury]]);

for i in FilterAllUnits([[f_side, 3], [f_btype, b_warehouse]]) do
    begin
    SetResourceType(GetBase(i), mat_cans, 1000);
    SetResourceType(GetBase(i), mat_oil, 300);
    end;

uc_side := 3;
uc_nation := 3;
skill := [2, 2, 3][Difficulty];

Pokryshkin := PrepareUnit('Pokryshkin', false);
PlaceUnitXY(Pokryshkin, 63, 21, false);
ComHold(Pokryshkin);

InitHc;

for i in tw do
    begin
    PlaceWeaponTurret(i, [ru_heavy_machine_gun, ru_gatling_gun][rand(1,2)]);
    PrepareSolBun(skill, i);
    end;

for i in bar do
    begin
    PrepareHuman(false, 1, skill);
    PlaceHumanInUnit(CreateHuman, i);
    end;

spot_xy := [ [100,9], [135,60], [41,6],  [22,9], [84,14] ];
vehicles := [ ];

for i in spot_xy do
    begin
    veh := PrepareTank(3, 3, ru_medium_tracked, engine_combustion, control_manual, [ru_heavy_machine_gun, ru_gatling_gun, ru_gun][rand(1,3)], 100);
    vehicles := Insert(vehicles, vehicles+1, veh);
    SetDir(veh, 3);
    PlaceUnitXY(veh, i[1], i[2], false);

    PrepareHuman(false, 3, skill);
    PlaceHumanInUnit(CreateHuman, veh);
    end;

for i = 1 to [5, 7, 8][Difficulty] do
    begin
    PrepareHuman(false, 1, skill);
    un := CreateHuman;
    PlaceUnitArea(un, forestGuardArea, false);
    ru_forest := ru_forest ^ un;
    end;

for i = 1 to [2, 3, 4][Difficulty] do
    begin
    PrepareHuman(false, 1, skill);
    un := CreateHuman;
    PlaceUnitArea(un, forestSouthGuardArea, false);
    ru_patrol := ru_patrol ^ un;
    end;

for i = 1 to 2 do
    begin
    PrepareHuman(false, 1, skill);
    un := CreateHuman;
    PlaceUnitXYR(un, 39, 12, 3, false);
    ru_spec_patrol := ru_spec_patrol ^ un;
    end;

ru_vehicles := vehicles;
ru_firepoints_south := [ [ [131,121], [113,90], [93,62] ],
                         [ [106,54],  [120,80], [143,120] ],
                         [ [154,116], [140,93], [130,58] ],
                         [ [105,106], [134,98], [159,113] ],
                       ];
End;