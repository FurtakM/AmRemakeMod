On EnterVehicle(vehicle, human) Do
begin
    if vehicle = powellCargo and GetSide(human) = 4 then
    begin
        ComTransport(PowellCargo, PowellDepot, 1);
        AddComMoveXY(PowellCargo, 68, 100);
        AddComUnload(PowellCargo);
        AddComMoveXY(PowellCargo, 41, 61);
    end;
end;

On UpgradeComplete(building) Do
var i, powellPeople;
begin
    
    powellPeople = FilterAllUnits([[f_side, 4], [f_type, unit_human], [f_outside], [f_not, [f_hastask]]]) diff [Powell];
    
    if GetSide(building) = 4 then
        if FilterAllUnits([[f_side, 4], [f_btype, b_lab_full]]) = 1 then
        begin
            for i = 1 to 3 do
            begin
                AddComEnterUnit(UnitFilter(powellPeople[i], [f_not, [f_class, 2]]), building);
                AddComChangeProfession(powellPeople[i], 4);
            end; 
              ResAllTechs(4, building, 1);
        end;


    if GetSide(building) = 8 then
    begin
        if FilterAllUnits([[f_side, 8], [f_btype, b_workshop], [f_nation, 3]]) = 1 and GetNation(building) = 3 then
        begin
            ComBuild(Kozlov, b_ext_siberium, 74, 12, 3);
            AddComEnterUnit(Kozlov, FilterAllUnits([[f_side, 4], [f_btype, b_factory], [f_nation, 3]])[1]);
            ComConstruct(FilterAllUnits([[f_side, 4], [f_btype, b_factory], [f_nation, 3]])[1], ru_heavy_tracked, 3, 3, ru_siberium_rocket);
        end;
    end;

end;

On BuildingComplete(building) Do
var powellPeople;
begin
    if GetSide(building) = 4 and GetBType(building) = b_ext_gun then
    begin

        powellPeople = FilterAllUnits([[f_side, 4], [f_type, unit_human], [f_outside], [f_not, [f_hastask]]]) diff [Powell];

        PlaceWeaponTurret(FilterAllUnits([[f_side, 4], [f_type, unit_building], [f_btype, b_bunker]])[1], us_heavy_gun);
        PlaceWeaponTurret(FilterAllUnits([[f_side, 4], [f_type, unit_building], [f_btype, b_bunker]])[2], us_double_gun); 
        ComEnterUnit(powellPeople[1], FilterAllUnits([[f_side, 4], [f_type, unit_building], [f_btype, b_bunker]])[1]);
        ComEnterUnit(powellPeople[2], FilterAllUnits([[f_side, 4], [f_type, unit_building], [f_btype, b_bunker]])[2]);
    end;

    if GetSide(building) = 8 then
    begin
        if GetBType(building) = b_workshop then ComUpgrade(building);
    end;
end;

On EnterBuilding(building, human) Do
begin

end;

On BehemothConstructed(vehicle) Do
begin
    if GetSide(vehicle) = 6 then ruBehs = ruBehs ^ vehicle;
end;

On VehicleConstructed(vehicle, building) Do
begin
    if vehicle = powellVeh then ComExitVehicle(UnitsInside(vehicle));

    if GetSide(vehicle) = 4 and powellAttacks = 2 and GetWeapon(vehicle) = us_heavy_gun then
    begin
         ComExitBuilding(amUnitsAttack[5]);
         wait(0$1);
         AddComEnterUnit(amUnitsAttack[5], AmFact);
         AddComChangeProfession(amUnitsAttack[5], 3);
    end;

    if GetSide(vehicle) = 4 and powellAttacks = 2 and GetWeapon(vehicle) = us_light_gun then
    begin
        powellVeh = vehicle;
        ComMoveXY(vehicle, 49, 74);
    end;

    if GetSide(vehicle) = 8 and GetNation(vehicle) = 2 and GetType(vehicle) = unit_vehicle then
    begin
        legionVehicles = legionVehicles ^ vehicle;
        ComLinkTo(vehicle, arControler[1]);
    end;

    if GetSide(vehicle) = 3 and GetNation(vehicle) = 3 and GetType(vehicle) = unit_vehicle then
        RussianVehicles = RussianVehicles ^ vehicle;

    if GetSide(vehicle) = 7  and GetType(vehicle) = unit_vehicle then
    begin
        AllianceVehicles = AllianceVehicles ^ vehicle;
        if GetControl(vehicle) = 2 then ComLinkTo(vehicle, allianceControler[rand(1, 3)]);
    end;

end;

On LeaveVehicle(vehicle, human) Do
begin
    if GetSide(vehicle) = 4 and  powellAttacks = 1  then
    begin
        ComEnterUnit(human, AmFact);
        if GetSex(human) = sex_male and blockEvent1 = 0 then 
        begin
            blockEvent1 = 1;
            startAttacks = 1;
            Say(human, 'D4a-Sol1-1');
            Say(Powell, 'D4a-Pow-1');
            Say(human, 'D4a-Sol1-2');
            Say(Powell, 'D4a-Pow-2');
        end;
    end;

end;


On UnitDestroyed(unit) Do
var i;
begin
    if GetSide(unit) = 4 and GetType(unit) = unit_human then
    begin
        for i in amUnitsAttack do 
            if i = unit then amUnitsAttack = amUnitsAttack diff [unit];
    end;

    if GetSide(unit) = 8 and GetType(unit) = unit_vehicle and GetNation(unit) = 2 then
    begin
        for i in legionVehicles do 
            if i = unit then legionVehicles = legionVehicles diff [unit];
    end;

    if GetSide(unit) = 3 and GetType(unit) = unit_vehicle  then
    begin
        for i in RussianVehicles do
            if i = unit then RussianVehicles = RussianVehicles diff [unit];
    end;

    if GetSide(unit) = 7 and GetType(unit) = unit_vehicle  then
    begin
        for i in AllianceVehicles do
            if i = unit then AllianceVehicles = AllianceVehicles diff [unit];
    end;

end;