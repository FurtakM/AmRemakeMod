// Set AI targets
Every 3$0 do
begin
enable;
LegionTarget = [1, 3][Rand(1, 2)];

if FilterAllUnits([[f_side, 8], [f_type, unit_human]]) > 1 then RussianTarget = [1, 8][Rand(1, 2)]
else RussianTarget = 1;

if FilterAllUnits([[f_side, 8], [f_type, unit_human]]) > 1 then AllianceTarget = [1, 3, 8][Rand(1, 3)]
else AllianceTarget = [1, 3][Rand(1, 2)];

end;

// Powell base
Every 0$1 trigger powellBuildBase = 1 do 
var i, powellPeople;
begin 
    for i = 1 to 4 do
    begin
        AddComEnterUnit(amUnitsAttack[i], AmFact);
        AddComChangeProfession(amUnitsAttack[i], 3);
    end;

    wait(0$1);

    powellPeople = FilterAllUnits([[f_side, 4], [f_type, unit_human], [f_outside], [f_not, [f_hastask]]]) diff [Powell];

    ComEnterUnit(powellPeople[1], PowellCargo);

    SetClass(powellPeople[2], 2);
    SetClass(powellPeople[3], 2);

    SetResourceVisibility(30, 49, 4);

    AddComUpgradeLab(AmLab, b_lab_weapon);
    AddComUpgradeLab(AmLab, b_lab_siberium);

    ComBuild(FilterAllUnits([[f_side, 4], [f_class, 2]]), b_siberite_mine, 30, 49, 1);
    AddComBuild(FilterAllUnits([[f_side, 4], [f_class, 2]]), b_bunker, 55, 56, 4);
    AddComBuild(FilterAllUnits([[f_side, 4], [f_class, 2]]), b_bunker, 63, 71, 4);
    AddComBuild(FilterAllUnits([[f_side, 4], [f_class, 2]]), b_siberite_power, 40, 72, 4); 
    AddComBuild(FilterAllUnits([[f_side, 4], [f_class, 2]]), b_solar_power, 45, 73, 4);
    AddComBuild(FilterAllUnits([[f_side, 4], [f_class, 2]]), b_ext_rocket, 37, 50, 2);
    AddComBuild(FilterAllUnits([[f_side, 4], [f_class, 2]]), b_ext_gun, 41, 51, 3);
    AddComBuild(FilterAllUnits([[f_side, 4], [f_class, 2]]), b_ext_siberium, 38, 54, 1);
end;

Every 1 trigger IsInArea(PowellCargo, CargoParking) and powellUnloadCargo = 1 do
begin
  ComExitVehicle(UnitsInside(PowellCargo));
  ComUpgrade(PowellDepot);
end;


// Legion
Every 0$1 trigger legionAI = 1 do 
begin
    enable;
    RepairAndRebuildBase(8, ArDep2, LegionBaseArea, legionDestroyedBuilds);
    HealUnitInBase(8, LegionHealArea, LegionBaseArea, [ArLab1]);
    AttackNearBuildings(8, 1);
    EnterBunkerOrBarrack(8, LegionBaseArea);
end;

//Every 0$1 trigger legionAI = 1 and [IsDead(ArDep2) or IsDying(ArDep2) or GetSide(ArDep2) = 1] and [IsDead(ArDep2) or  IsDying(ArDep1) or GetSide(ArDep1) = 1] do DefendDepot(8, ArDep2);

Every 3$0 trigger legionAI = 1 and missionStage >= 1 and LegionVehicles = [] and IsOk(ArFact) and GetSide(ArFact) = 8 do 
begin
    enable;
    BuildingVehicle(8, ArFact, [5, 6, 7][Difficulty], [27, 28, 25, 29], 3, 2, 14);
end;

Every 0$1 trigger legionAI = 1 and missionStage >=1 and LegionVehicles = [5, 6, 7][Difficulty] do 
begin
    enable;
    AttackVehicles(LegionTarget, 1);
end;

// Magenta ru
Every 0$1 do
begin
enable;
TimeLapsersAI(6, 1);
end;

// Russian
Every 0$1 trigger RussianAI = 1 do 
begin
    enable;
    RepairAndRebuildBase(3, RuDep1, RussianBaseArea, russianDestroyedBuilds);
    HealUnitInBase(3, RussianHealArea, RussianBaseArea, [RusLab1, RusLab2]);
    AttackNearBuildings(3, 1);
    TimeLapsersAI(3, RussianTarget);
    EnterBunkerOrBarrack(3, RussianBaseArea);
    CraneRepairBuild(3, RussianBaseArea, russianDestroyedBuilds);
end;

Every 3$0 trigger RussianAI = 1 and missionStage >= 3 and RussianVehicles = [] and IsOk(RusFact2) and GetSide(RusFact2) = 3 and IsOk(RusFact1) and GetSide(RusFact1) = 3 do
begin
    enable;
    BuildingVehicle(3, RusFact1, [4, 5, 6][Difficulty], [45, 46, 47, 49], 3, 3, 24);
    BuildingVehicle(3, RusFact2, [4, 5, 6][Difficulty], [45, 46, 47, 49], 3, 3, 24);
end;

Every 0$1 trigger RussianAI = 1 and missionStage >=3 and RussianVehicles = [8, 10, 12][Difficulty] do
begin
    enable;
    ComEnterUnit(FilterAllUnits([[f_side, 3], [f_type, unit_vehicle], [f_not, [f_weapon, 48]], [f_not, [f_hastask]]]), [rutp1, rutp2][Rand(1, 2)]);
    wait(0$20);
    AttackVehicles(RussianTarget, 2);
end;

// Alliance
Every 0$1 trigger AllianceAI = 1 do 
begin
    enable;
    RepairAndRebuildBase(7, AllDep, AllianceBaseArea, allianceDestroyedBuilds);
    HealUnitInBase(7, AllianceHealArea, AllianceBaseArea, [AllLabA, AllLabR1, AllLabR2]);
    AttackNearBuildings(7, 1);
    TimeLapsersAI(7, AllianceTarget);
    EnterBunkerOrBarrack(7, AllianceBaseArea);
end;

Every 3$0 trigger AllianceAI = 1 and missionStage >= 4 and AllianceVehicles = [] and IsOk(Allfab1) and GetSide(Allfab1) = 7 and IsOk(Allfab2) and GetSide(Allfab2) = 7 do
begin
    enable;
    BuildingVehicle(3, Allfab1, [3, 3, 4][Difficulty], [9, 10, 7], 3, 2, 5);
    BuildingVehicle(3, Allfab2, [2, 2, 3][Difficulty], [45, 46, 47, 49], 3, 3, 24);
end;

Every 0$1 trigger AllianceAI = 1 and missionStage >=4 and AllianceVehicles = [5, 5, 7][Difficulty] do
var i;
begin
    for i in AllianceVehicles do TeleportUnit(i, 117, 112, 10, True);
    AttackVehicles(AllianceTarget, 3);
    enable;
end;

Every 3$0 trigger AllianceAI = 1 and missionStage >=4 do
var tmp;
begin
     enable;

     tmp = FilterAllUnits([[f_side, 1], [f_type, unit_building], [f_or, [f_btype, b_siberite_mine], [f_btype, b_siberite_power], [f_btype, 21]]]);
     tmp = tmp ^ FilterAllUnits([[f_side, 1], [f_type, unit_vehicle], [f_engine, 3]]);

     if tmp = [] then exit;

     if tmp > 0 then SetLives(tmp[Rand(1, tmp)], 0);

end;


// Remote vehs legion and alliance
Every 0$1 trigger LegionAI = 1 and FilterAllUnits([[f_side, 8], [f_type, unit_vehicle], [f_not, [f_linked]]]) > 0 do
var i;
begin
     enable;

     for i in FilterAllUnits([[f_side, 8], [f_type, unit_vehicle], [f_not, [f_linked]]]) do
         ComLinkTo(i, arControler[1]);
end;

Every 0$1 trigger AllianceAI = 1 and FilterAllUnits([[f_side, 7], [f_type, unit_vehicle], [f_not, [f_linked]]]) > 0 do
var i;
begin
     enable;

     for i in FilterAllUnits([[f_side, 7], [f_type, unit_vehicle], [f_not, [f_linked]]]) do
         ComLinkTo(i, allianceControler[rand(1, 3)]);
end;