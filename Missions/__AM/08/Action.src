Every 1 do 
var i;
begin
    //InGameOn;

    CenterOnXY(53, 40);

    wait(0$5);

    CenterNowOnUnits(Donaldson);

    Say(Donaldson, 'D1-Don-1');
    SayRadio(Harisson, 'D1-Har-1');
    Say(Donaldson, 'D1-Don-2');
    SayRadio(Harisson, 'D1-Har-2');

    CenterOnXY(53, 40);

    repeat
        wait(0$1);
        for i in FilterAllUnits([[f_side, 6]]) do ComAgressiveMove(i, 54, 20);
    until FilterAllUnits([[f_side, 6], [f_type, unit_human], [f_lives, 250]]) = 0;

    InGameOff;

    SaveForQuickRestart;
end;

// Arrive two soldiers from Delta base
Every 3$0 do 
begin
    PrepareSupportFromDelta;
    firstAttack = 1;
    DialogRandom(FilterUnitsInArea(GammaSupport, [[f_side, 1], [f_type, unit_human]]), 'D2-Sol1-1', 'D2-FSol1-1');
    If IsOK(Donaldson) then Say(Donaldson, 'D2-Don-1');
end;

Every 0$1 trigger firstAttack = 1 do  RespawnAttackInfantry(6, 3, [rand(5, 6), 6, rand(5, 7)][Difficulty], 0, 0, 0, 0, rand(RuSolMinLevel, RuSolMaxLevel), RussianAttackSpawn, RussianHealArea, 1, rand(1,3));
Every 0$1 trigger firstAttack = 1 do  RespawnAttackVehicle(RussianAttackSpawn, 6, rand(3, 5), rand(RuSolMinLevel, RuSolMaxLevel), 3, 1, 3, [42, 43, 44, 46], [], [], 1);


// Spawn John
Every 9$0 do
begin
    PrepareMacmillanSquad;
    Say(JMM, 'D3-JMM-1');

    If IsOK(Donaldson) then
    begin
        Say(Donaldson, 'D3-Don-1');
    end else
    begin
        DialogRandom(FilterUnitsInArea(GammaSupport, [[f_side, 1], [f_type, unit_human], [f_sex, 1]]), 'D3-Sol1-1', 'D3-Sol1-1');
        Say(JMM, 'D3-JMM-2');
    end; 
    If IsOK(Joan) then
    begin
        Say(Joan, 'D3a-Joan-1');
        Say(JMM, 'D3a-JMM-1');
    end; 

    DialogRandom(FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_sex, 1], [f_or, [f_class, 1], [f_class, 2], [f_class, 3], [f_class, 4]]]) diff [JMM], 'DCa-Sol1-1', 'DCa-Sol1-1');
    Say(JMM, 'DCb-JMM-1');
    DialogRandom(FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_sex, 1], [f_or, [f_class, 1], [f_class, 2], [f_class, 3], [f_class, 4]]]) diff [JMM], 'DCb-Sol1-1', 'DCb-Sol1-1');
    Say(JMM, 'DCb-JMM-2');

    JMMArrive = 1;
    secondAttack = 1;

end;

// Dialog with Powell about Harrison
Every 13$0 trigger JMMArrive = 1  do 
begin
    DialogueOn;
    SayRadio(Powell, 'D4-Pow-1');
    Say(JMM, 'D4-JMM-1');
    SayRadio(Powell, 'D4-Pow-2');
    Say(JMM, 'D4-JMM-2');
    SayRadio(Powell, 'D4-Pow-3');
    DialogueOff;
end;

// Dialog about support
Every 20$0 trigger JMMArrive = 1 do
begin
    DialogueOn;
    SayRadio(Powell, 'D4a-Pow-1');
    DialogueOff;
end;

// Sikorsky
Every 30$0 trigger JMMArrive = 1 do
begin
    PrepareSikorsky;
    PrepareSikorskyArmy;
    PrepareSikorskyTanks;
end;