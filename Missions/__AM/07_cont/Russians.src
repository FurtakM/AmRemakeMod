Export ru_rebuild_list;
Export Function PrepareRussians;
var i, j, r, un, skill, filter, tmp, dep_list, lab_list, fac_list, breastworks_list, bunker_list, turret_list, weapons_list, personel_counter;
begin
ru_rebuild_list := [];

uc_side := 3;
uc_nation := 3;

if Difficulty > 1 then
   begin
   bc_type := b_breastwork;
   CreateAndPlaceBuildingXYD(78, 74, 2);
   CreateAndPlaceBuildingXYD(171, 193, 4);

   bc_type := b_turret;
   CreateAndPlaceBuildingXYD(129, 96, 3);

   if Difficulty > 2 then
      begin
      RemoveUnit(HexInfo(83, 141));
      RemoveUnit(HexInfo(78, 133));

      CreateAndPlaceBuildingXYD(83, 141, 2);
      CreateAndPlaceBuildingXYD(78, 133, 2);
      CreateAndPlaceBuildingXYD(145,112, 3);
      end;
   end;

dep_list := FilterAllUnits([[f_side, 3], [f_or, [f_btype, b_depot], [f_btype, b_warehouse]]]);
lab_list := FilterAllUnits([[f_side, 3], [f_or, [f_btype, b_lab], [f_btype, b_lab_half], [f_btype, b_lab_full]]]);
fac_list := FilterAllUnits([[f_side, 3], [f_btype, b_factory]]);
breastworks_list := FilterAllUnits([[f_side, 3], [f_btype, b_breastwork]]);
bunker_list := FilterAllUnits([[f_side, 3], [f_btype, b_bunker]]);
turret_list := FilterAllUnits([[f_side, 3], [f_btype, b_turret]]);

weapons_list := [[ru_heavy_machine_gun, 5], [ru_gatling_gun, 50], [ru_gun, 30], [ru_heavy_gun, 15]];
personel_counter := [0, 4, 4, 3];
skill := [4, 5, 6][Difficulty];

SetBName(ru_dep_main, 'am_tukh');

for i in FilterAllUnits([f_type, unit_building]) do
    SetBLevel(i, [4, 5, 6][Difficulty]);

for i in dep_list do
    begin
    SetResourceType(GetBase(i), mat_cans, 1000);
    SetResourceType(GetBase(i), mat_oil, 1000);
    end;

InitHc;

// soldiers in towers [50%, 70%, 85%]
tmp := bunker_list * [50, 70, 85][Difficulty] div 100;
personel_counter := Replace(personel_counter, 1, tmp);

// sort
tmp := [];

for i in bunker_list do
    if i mod 10 + 1 < tmp then
       tmp := Insert(tmp, i mod rand(3,5) + 1, i)
      else
       tmp := Insert(tmp, tmp + 1, i);

bunker_list := tmp;

// place weapon on turrets
for i in bunker_list^turret_list do
    begin
    tmp := rand(0, 100);
    r := 0;

    for j in weapons_list do
        begin
        r := r + j[2];

        if r >= tmp then
           begin
           PlaceWeaponTurret(i, j[1]);
           break;
           end;
        end;
    end;

// place personel in base
while (personel_counter[1]) do
    begin
    i := rand(1, bunker_list);

    PrepareHuman(false, 1, skill);
    PlaceHumanInUnit(CreateHuman, bunker_list[i]);

    bunker_list := Delete(bunker_list, i);
    personel_counter := Replace(personel_counter, 1, personel_counter[1] - 1);
    end;

for i = 1 to personel_counter[2] do
    begin
    PrepareHuman(false, 2, skill);
    PlaceHumanInUnit(CreateHuman, ru_dep_main);
    end;

for i = 1 to personel_counter[3] do
    begin
    PrepareHuman(false, 3, skill);
    PlaceHumanInUnit(CreateHuman, fac_list[1]);
    end;

for i = 1 to personel_counter[4] do
    begin
    PrepareHuman(false, 4, skill);
    PlaceHumanInUnit(CreateHuman, lab_list[1]);
    end;

for i in breastworks_list do
    begin
    r := GetDir(i);

    if r > 3 then
       r := r - 3
      else
       r := r + 3;

    PrepareHuman(false, 1, skill);
    un := CreateHuman;
    SetDir(un, r);
    PlaceHumanInUnit(un, i);
    end;

r := [4, 5, 6][Difficulty];

for i = 1 to r do
    begin
    un := PrepareTank(3, 3, ru_medium_tracked, engine_combustion, control_manual, [ru_gatling_gun, ru_gun, ru_heavy_machine_gun][i mod 3 + 1], 100);
    SetDir(un, rand(0, 5));
    PlaceUnitArea(un, parkingArea, false);
    end;
    
End;

// Ru script
// Done: 
// - Wycofywanie zolnierzy z uszkodzonych bunkrow/wiez
// 
// Todo:
// - Elastyczna obrona pojazdami 
// - Posilki z Trockiego
// - Naprawa budynkow w bazie
// - Odbudowywanie niektorych bunkrow w bazie (zeby wkurzyc Livida)
// - W razie ataku glownego magazynu wszyscy zolnierze przychodza go bronic! (if GetLives(ru_dep_main) < 800)
//
//
// Dragon zapcha³ kibel i zrzuci³ na kolege
Every 0$2 trigger FilterUnitsInArea(baseArea, [f_type, unit_building]) do
var i, filter, engs;
begin
enable;

filter := FilterUnitsInArea(baseArea, [[f_not, [f_ok]], [f_type, unit_building]]);
engs := FilterAllUnits([[f_side, 3], [f_class, 2], [f_ok]]);

if not filter or not engs then
   exit;


End;