Export Kurt, Kozlov, Friend;
Export powellBomb;
Export Function PrepareLegion;
var i, side, un, tmp;
begin
side := 8;

uc_side := side;
uc_nation := 2;

for i in FilterAllUnits([[f_side, side], [f_type, unit_building]]) do
    SetBLevel(i, 10);

if KurtStatus then
   Kurt := PrepareUnit('Kurt', false, '')
else
   Kurt := PrepareUnit('AltKurt', (not debug), '');

if not Kurt then
   begin
   InitHc;
   PrepareSoldier(sex_male, 10);
   Kurt := CreateHuman;
   end;

PlaceHumanInUnit(Kurt, ar_depot1);

Kozlov := PrepareUnit('Kozlov', false, '');
PlaceHumanInUnit(Kozlov, FilterAllUnits([[f_side, 8], [f_nation, 3], [f_btype, b_lab_full]])[1]);

tmp := FilterAllUnits([[f_side, side], [f_btype, b_bunker], [f_empty]]);

for i = 1 to 10 do
    begin
    uc_nation := nation_nature;
    hc_class := class_apeman_soldier;
    hc_gallery := '';
    hc_name := '';

    un := CreateHuman;
    PlaceHumanInUnit(un, tmp[tmp-i]);
    end;

tmp := CreateCharacterSet('12_kurt_squad');

if tmp then
   for i in tmp do
       PlaceUnitArea(i, legionBaseArea, false);

// PrepareBase(base_dep, area, name, skill, sources, personel);
PrepareBase(ar_depot1, legionBaseArea, '', [8, 9, 10][Difficulty], [3000, 500, 150], [16, 6, 6, 8]);

mc_bases := Replace(mc_bases, 3, FilterAllUnits([[f_side, side], [f_nation, 2], [f_not, [f_type, unit_vehicle]]]) diff Kurt);

// friend
PrepareMechanic(sex_male, 7);
Friend := CreateHuman;

// bomb for Powell
PrepareVehicle(ar_half_tracked, engine_siberite, control_manual, ar_selfpropelled_bomb, 100);
powellBomb := CreateVehicle;
PlaceUnitXY(powellBomb, 90, 51, false);
End;

Export Function RebuildKozlovFactory;
var fac, lab;
begin
if IsLive(kozlov_fac) then
   exit;

ComExitBuilding(Kozlov);

if GetClass(Kozlov) <> 2 then
   begin
   AddComEnterUnit(Kozlov, ar_depot1);
   AddComChangeProfession(Kozlov, class_engineer);
   AddComExitBuilding(Kozlov);
   end;

AddComBuild(Kozlov, b_workshop, 93, 32, 3);

repeat
 wait(0$1);
until FilterAllUnits([[f_side, 8], [f_btype, b_factory], [f_nation, 3], [f_constructed]]);

fac := FilterAllUnits([[f_side, 8], [f_btype, b_factory], [f_nation, 3], [f_constructed]])[1];

if not FilterAllUnits([[f_side, 8], [f_nation, 3], [f_btype, b_ext_siberium]]) then
   begin
   AddComBuild(Kozlov, b_ext_siberium, 97, 36, 5);
   repeat
    wait(0$1);
   until FilterAllUnits([[f_side, 8], [f_nation, 3], [f_btype, b_ext_siberium], [f_constructed]]);
   end;

if not FilterAllUnits([[f_side, 8], [f_nation, 3], [f_btype, b_ext_rocket]]) then
   begin
   AddComBuild(Kozlov, b_ext_rocket, 89, 32, 1);
   repeat
    wait(0$1);
   until FilterAllUnits([[f_side, 8], [f_nation, 3], [f_btype, b_ext_rocket], [f_constructed]]);
   end;

lab := kozlov_lab;

if not lab then
   exit; // wtf?

AddComEnterUnit(Kozlov, lab[1]);
AddComChangeProfession(Kozlov, 4);

ComResearch(lab[1], tech_sibFiss);

repeat
 wait(0$1);
until GetTech(tech_sibFiss, 8) = state_researched;

ComExitBuilding(Kozlov);
AddComEnterUnit(Kozlov, fac);
AddComChangeProfession(Kozlov, 3);

ComConstruct(fac, ru_heavy_wheeled, engine_siberite, control_manual, ru_siberium_rocket);
End;