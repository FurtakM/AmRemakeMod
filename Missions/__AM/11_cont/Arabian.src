Export ar_force_west, ar_force_south, ar_force_north, ar_force_east;
Export ar_force_tmp, ar_vehicles_tmp;
Export Saliba;

Export Function PrepareArabian;
var i, d, un, skill;
begin
skill := [6, 7, 8][Difficulty];

ComLinkToBase([ar_dep_e], 141); 
Wait(1);

uc_side := 2;
uc_nation := 2;

// control_towers 156 15
vc_chassis := ar_half_tracked;
vc_engine := engine_siberite;
vc_control := control_manual;
vc_weapon := ar_control_tower;

for i = 1 to 3 do
    begin
    PrepareHuman(false, class_mechanic, skill);

    un := CreateVehicle;
    SetDir(un, rand(0, 5));
    PlaceUnitXYR(un, 156, 15, 6, false);
    PlaceHumanInUnit(CreateHuman, un);
    end;

ar_force_north := PrepareBase(ar_dep_n, 'dammam', skill, [3000, 500, 300], [-1, -1, -1, 4]);
ar_force_north := ar_force_north ^ PrepareBase(ar_dep_n2, '', skill, [100, 60, 0], [-1, 4, 0, 0]);
ar_force_west := PrepareBase(ar_dep_w, 'jeddah', skill, [700, 300, 10], [-1, 5, 0, 2]);
ar_force_south := PrepareBase(ar_dep_s, 'riyadh', skill, [500, 60, 0], [4, 2, 3, 1]);
ar_force_east := PrepareBase(ar_dep_e, '', skill, [500, 50, 0], [-1, 3, 1, 2]);

mc_bases := [ar_force_north, ar_force_west, ar_force_east];

for i in FilterAllUnits([[f_side, 2], [f_btype, b_breastwork], [f_empty]]) do
    begin
    if GetBase(i) then
       continue;

    d := GetDir(i);

    if d < 3 then
       d := d + 3
      else
       d := d - 3;

    PrepareHuman(false, class_mortar, skill);
    un := CreateHuman;

    SetDir(un, d);
    PlaceHumanInUnit(un, i);
    end;

ar_force_tmp := [];

PrepareHuman(sex_male, 1, skill);
hc_gallery := 'SecondCharsGal';
hc_face_number := 4;
ar_force_tmp := Replace(ar_force_tmp, 1, CreateHuman);

PrepareHuman(sex_female, 4, skill);
hc_gallery := 'SecondCharsGal';
hc_face_number := 15;
ar_force_tmp := Replace(ar_force_tmp, 2, CreateHuman);


PrepareHuman(sex_female, 4, skill);
hc_gallery := '';
ar_force_tmp := Replace(ar_force_tmp, 3, CreateHuman);

hc_sex := sex_male;
hc_class = 11;
hc_gallery = 'sandar';
hc_face_number = 33;
hc_name = 'Thabit Muhair Saliba';
hc_skills = [0,0,0,0];

Saliba = CreateHuman;
PlaceHumanInUnit(Saliba, ar_dep_s);


// prepare vehicles
ar_vehicles_tmp := [];

for i = 1 to 4 do
    begin
    PrepareVehicle([ar_medium_trike, ar_half_tracked][rand(1,2)],
                   [engine_combustion, engine_solar][rand(1,2)],
                   control_manual,
                   [ar_gatling_gun, ar_gun, ar_flame_thrower][rand(1,3)],
                   rand(60, 100));

    un := CreateVehicle;
    ar_vehicles_tmp := Replace(ar_vehicles_tmp, ar_vehicles_tmp+1, un);
    SetDir(un, rand(0,5));
    PlaceUnitXYR(un, 124, 141, 6, false);
    end;

InitHc;
End;